buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/repo' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/repo' }
}

task syncTask(type: Sync) {
    from "src/main/res/values-zh-rTW"
    into "src/main/res/values-zh-rHK"
}

preBuild.dependsOn('syncTask')

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 11
        versionName '0.1.5'
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS_PLUSTIMER
            keyPassword RELEASE_KEY_PASSWORD_PLUSTIMER
        }
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        crashlytics {
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
        }
        noCrashlytics {
            versionName = android.defaultConfig.versionName + "-NOCRASHLYTICS"
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    compile project(':libraries:hlistviewlibrary')
    compile 'com.jjoe64:graphview:3.+'
    compile 'com.android.support:support-v13:21.+'
    compile 'com.github.cubing:tnoodle-android:0.+@aar'
    compile 'com.caverock:androidsvg:1.2.1'
    compile 'com.android.support:support-annotations:21.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.gson:gson:2.+'
    compile('com.crashlytics.sdk.android:crashlytics:2.0.0@aar') {
        transitive = true;
    }
}
