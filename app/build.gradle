buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.fernandocejas.frodo:frodo-plugin:0.8.2'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'com.jakewharton.hugo'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

task syncTask(type: Sync) {
    from "src/main/res/values-zh-rTW"
    into "src/main/res/values-zh-rHK"
}

preBuild.dependsOn('syncTask')

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.pluscubed.plustimer"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 24
        versionName '0.3.0-beta'
        manifestPlaceholders = [appName: "@string/app_name"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_PLUSTIMER
                keyPassword RELEASE_KEY_PASSWORD_PLUSTIMER
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            manifestPlaceholders = [appName: "plusTimer DEBUG"]
            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        normal {
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
        }
        noPermissions {
            versionName = android.defaultConfig.versionName + "-NoPermissions"
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

retrolambda {
    jvmArgs '-noverify'
}

dependencies {

    debugCompile 'im.dino:dbinspector:3.2.3@aar'

    //compile 'com.firebase:firebase-client-android:2.5.2'
    //compile 'com.firebaseui:firebase-ui:0.3.1'

    compile 'com.couchbase.lite:couchbase-lite-android:1.2.0'
    compile 'com.couchbase.lite:couchbase-lite-android-forestdb:1.2.0'


    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'

    compile 'com.jjoe64:graphview:3.1.4'

    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:support-v13:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:preference-v14:23.2.1'

    androidTestCompile 'com.android.support:support-annotations:23.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "org.robolectric:robolectric:3.1-SNAPSHOT"

    compile('com.github.cubing:tnoodle-android:0.8.9@aar') {
        transitive = true;
    }
    compile 'com.caverock:androidsvg:1.2.1'

    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.github.afollestad.material-dialogs:core:0.8.5.6'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
}
