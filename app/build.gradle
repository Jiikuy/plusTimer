buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.hamsterksu:android-appversion-gradle-plugin:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'versionPlugin'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

task syncTask(type: Sync) {
    from "src/main/res/values-zh-rTW"
    into "src/main/res/values-zh-rHK"
}

preBuild.dependsOn('syncTask')

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        applicationId "com.pluscubed.plustimer"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 22
        versionName '0.2.4'
        manifestPlaceholders = [appName: "@string/app_name"]
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_PLUSTIMER
                keyPassword RELEASE_KEY_PASSWORD_PLUSTIMER
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            manifestPlaceholders = [appName: "plusTimer DEBUG"]
            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        normal {
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
        }
        noPermissions {
            versionName = android.defaultConfig.versionName + "-NoPermissions"
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    compile 'com.jjoe64:graphview:3.+'
    compile 'com.android.support:appcompat-v7:22.+'
    compile 'com.android.support:support-v13:22.+'
    compile 'com.android.support:recyclerview-v7:22.+'
    compile 'com.github.cubing:tnoodle-android:0.+@aar'
    compile 'com.caverock:androidsvg:1.2.1'
    compile 'com.android.support:support-annotations:22.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.gson:gson:2.+'
    compile 'com.afollestad:material-dialogs:0.+'
    compile 'com.rengwuxian.materialedittext:library:2.+'
    compile('com.crashlytics.sdk.android:crashlytics:2.+@aar') {
        transitive = true;
    }
}

versionPlugin {
    buildTypesMatcher = 'release'

    supportBuildNumber = false

    fileNameFormat = '${projectName}_$versionName'
}
